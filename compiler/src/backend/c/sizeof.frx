import "c.frx";
import "../core/mangling.frx";
import "../../frontend/symbols/enum.frx";
import "../../frontend/symbols/struct.frx";
import "../../frontend/syntax/internal_func_call.frx";

extern struct FILE;

extern
{
    i32 fprintf(mut FILE* stream, char* format, ...);
}

export void transpile_sizeof(Transpiler* transpiler, ASTSizeof* sizeof_node)
{
    frx::assert(transpiler != nullptr);

    frx::assert(sizeof_node != nullptr);

    fprintf(transpiler->file, "sizeof(");

    if(sizeof_node->primitive_type != TokenType::ERROR)
    {
        fprintf(transpiler->file, "%s",
            token_type_to_str(sizeof_node->primitive_type));
    }
    else if(sizeof_node->enum_type != nullptr)
    {
        EnumSymbol* symbol = sizeof_node->enum_type;
        char* mangled_name = mangle_enum_name(symbol->scope, symbol->ns,
            symbol->name);

        fprintf(transpiler->file, "%s", mangled_name);
    }
    else
    {
        StructSymbol* symbol = sizeof_node->struct_type;
        char* mangled_name = mangle_struct_name(symbol->scope, symbol->ns,
            symbol->name);

        fprintf(transpiler->file, "%s", mangled_name);
    }

    fprintf(transpiler->file, ")");
}
