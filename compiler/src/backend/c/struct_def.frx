import "c.frx";
import "type_specifier.frx";
import "../core/mangling.frx";
import "../../core/queue.frx";
import "../../frontend/ast/struct_def.frx";

extern struct FILE;

extern
{
    i32 fprintf(mut FILE* stream, char* format, ...);
}

void transpile_struct_field(Transpiler* transpiler, AST::StructField* field)
{
    frx::assert(transpiler != nullptr);

    frx::assert(field != nullptr);

    AST::TypeSpecifier* type = field->type;

    transpile_type_specifier(transpiler, type);
    fprintf(transpiler->file, "%s", field->name);
    transpile_type_specifier_array_dimensions(transpiler, type);
    fprintf(transpiler->file, ";\n");
}

export void transpile_struct_def(Transpiler* transpiler,
    AST::StructDef* struct_def)
{
    frx::assert(transpiler != nullptr);

    frx::assert(struct_def != nullptr);

    StructSymbol* symbol = &struct_def->symbol;

    char* mangled_name = mangle_struct_name(symbol->scope, symbol->ns,
        symbol->name);

    if(transpiler->mode == TranspilerMode::STRUCT_DECL)
    {
        fprintf(transpiler->file, "typedef struct %s", mangled_name);
        fprintf(transpiler->file, " ");
        fprintf(transpiler->file, "%s;\n", mangled_name);
    }
    else if(transpiler->mode == TranspilerMode::STRUCT_IMPL)
    {
        mut b8 should_transpile = true;

        mut usize i;
        for(i = 0; i < list_size(&symbol->fields); i = i + 1)
        {
            AST::StructField* field = list_get(&symbol->fields, i);
            AST::TypeSpecifier* type = field->type;
            if(type->struct_type == nullptr || type->pointer_level > 0)
            {
                continue;
            }

            if(!list_contains(&transpiler->transpiled_symbols,
                type->struct_type))
            {
                should_transpile = false;

                if(!Queue::contains(&transpiler->untranspiled_structs,
                    struct_def))
                {
                    Queue::enqueue(&transpiler->untranspiled_structs,
                        struct_def);
                }
            }
        }

        if(should_transpile)
        {
            fprintf(transpiler->file, "struct %s", mangled_name);
            fprintf(transpiler->file, "\n");
            fprintf(transpiler->file, "{\n");

            for(i = 0; i < list_size(&symbol->fields); i = i + 1)
            {
                AST::StructField* field = list_get(&symbol->fields, i);
                transpile_struct_field(transpiler, field);
            }

            fprintf(transpiler->file, "};\n");

            list_push(&transpiler->transpiled_symbols, symbol);
        }
    }
}
