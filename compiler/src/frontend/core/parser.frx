import "ast.frx";
/*export*/ import "lexer.frx";
import "namespace.frx";
import "scope.frx";
import "../symbols/enum.frx";
import "../symbols/func.frx";
import "../symbols/struct.frx";
import "../symbols/var.frx";
/*export*/ import "../../core/arena.frx";
import "../../core/list.frx";

export struct Parser
{
    Lexer lexer;

    ASTCompound* translation_unit;

    Token* token;

    Namespace* current_namespace;
    Scope* current_scope;

    List visible_parsers;

    b8 panic;
    b8 failed;

    Arena arena;

    EnumTable enum_table;
    StructTable struct_table;
    FuncTable func_table;
    VarTable var_table;
}

export void parser_init(Parser* parser, char* filepath)
{
    assert(parser != nullptr);

    assert(filepath != nullptr);

    lexer_init(&parser->lexer, filepath);

    parser->translation_unit = nullptr;

    parser->token = lexer_peek(&parser->lexer, 0);

    parser->current_namespace = namespace_create_global();
    parser->current_scope = scope_create(nullptr);

    list_init(&parser->visible_parsers);

    parser->panic = false;
    parser->failed = false;

    arena_init(&parser->arena, 1024 * 1024);

    enum_table_init(&parser->enum_table);
    struct_table_init(&parser->struct_table);
    func_table_init(&parser->func_table);
    var_table_init(&parser->var_table);
}

void parser_advance(Parser* parser)
{
    assert(parser != nullptr);

    lexer_next_token(&parser->lexer);
}

export void parser_eat(Parser* parser, TokenType type)
{
    if(parser->token->type != type)
    {
        log_error(parser_source_file(parser), parser_current_location(parser),
            "Expected '%s', but found '%s'!", token_type_to_str(type),
            token_type_to_str(parser->token->type));

        parser->failed = true;
        parser_panic(parser);
        return;
    }

    parser_advance(parser);
}

export b8 parser_loop_until(Parser* parser, TokenType type)
{
    assert(parser != nullptr);

    assert(type < TOKEN_TYPE_COUNT);

    return parser->token->type != type
        && parser->token->type != TOKEN_TYPE_ERROR
        && parser->token->type != TOKEN_TYPE_EOF;
}

void parser_panic(Parser* parser)
{
    assert(parser != nullptr);

    while(true)
    {
        switch(parser->token->type)
        {
            case TOKEN_TYPE_EOF:
            {

            }
            case TOKEN_TYPE_ERROR:
            {

            }
            case TOKEN_TYPE_SEMICOLON:
            {

            }
            case TOKEN_TYPE_RBRACE:
            {
                return;
            }
            default:
            {
                parser_advance(parser);

                break;
            }
        }
    }
}

export void parser_parse(Parser* parser)
{
    assert(parser != nullptr);

    parser->translation_unit = parse_translation_unit(parser);
}

b8 file_exists(char* filepath)
{
    assert(filepath != nullptr);

    FILE* file = fopen(filepath, "r");
    if(file == nullptr)
    {
        return false;
    }

    fclose(file);
    return true;
}

export Parser* parser_compile(Parser* parser, char* filepath)
{
    assert(parser != nullptr);

    assert(filepath != nullptr);

    char[4096] directory;
    strcpy(directory, parser_source_file(parser));
    char* last_slash = strrchr(directory, '/');
    char temp = last_slash[0] = '\0';

    char[4096] resolved_filepath;
    sprintf(resolved_filepath, "%s/%s", directory, filepath);

    char[4096] fullpath;
    realpath(resolved_filepath, fullpath);

    if(file_exists(fullpath))
    {
        compile(fullpath);
    }

    return parser_table_find(fullpath);
}

void parser_resolve_imports_recursive(List* visible_parsers, Parser* parser,
    List* visited_parsers)
{
    assert(parser != nullptr);

    assert(visible_parsers != nullptr);

    assert(visited_parsers != nullptr);

    usize i;
    for(i = 0; i < list_size(&parser->translation_unit->nodes); i = i + 1)
    {
        AST* ast = list_get(&parser->translation_unit->nodes, i);
        if(ast->type != AST_TYPE_IMPORT_STMT)
        {
            continue;
        }

        ASTImportStmt* import_stmt = ast->node;
        Parser* imported_parser = import_stmt->imported_parser;
        if(imported_parser == nullptr)
        {
            continue;
        }

        if(import_stmt->exported && !list_contains(visible_parsers,
            imported_parser))
        {
            list_push(visible_parsers, imported_parser);
        }

        if(!list_contains(visited_parsers, imported_parser))
        {
            list_push(visited_parsers, imported_parser);
            parser_resolve_imports_recursive(visible_parsers, imported_parser,
                visited_parsers);
        }
    }
}

export void parser_resolve_imports(Parser* parser)
{
    assert(parser != nullptr);

    List* visible_parsers = &parser->visible_parsers;

    List visited_parsers;
    list_init(&visited_parsers);

    usize i;
    for(i = 0; i < list_size(&parser->translation_unit->nodes); i = i + 1)
    {
        AST* ast = list_get(&parser->translation_unit->nodes, i);
        if(ast->type != AST_TYPE_IMPORT_STMT)
        {
            continue;
        }

        ASTImportStmt* import_stmt = ast->node;
        Parser* imported_parser = import_stmt->imported_parser;
        if(imported_parser == nullptr)
        {
            continue;
        }

        if(!list_contains(visible_parsers, imported_parser))
        {
            list_push(visible_parsers, imported_parser);
        }

        if(!list_contains(&visited_parsers, imported_parser))
        {
            list_push(&visited_parsers, imported_parser);
            parser_resolve_imports_recursive(visible_parsers, imported_parser,
                &visited_parsers);
        }
    }

    list_destroy(&visited_parsers);
}

export void parser_analyze(Parser* parser)
{
    assert(parser != nullptr);

    analyze_compound(parser, parser->translation_unit);
}

export void parser_next_token(Parser* parser)
{
    lexer_next_token(&parser->lexer);
}

export Token* parser_peek(Parser* parser, usize offset)
{
    assert(parser != nullptr);

    return lexer_peek(&parser->lexer, offset);
}

export EnumSymbol* parser_find_enum_symbol_r(Parser* parser, Namespace* ns,
    char* name)
{
    EnumSymbol* symbol = parser_find_enum_symbol(parser, ns, name);
    while(symbol == nullptr && ns != nullptr)
    {
        ns = ns->parent;
        symbol = parser_find_enum_symbol(parser, ns, name);
    }

    return symbol;
}

export EnumSymbol* parser_find_enum_symbol(Parser* parser, Namespace* ns,
    char* name)
{
    assert(parser != nullptr);

    EnumSymbol* symbol = enum_table_find(&parser->enum_table, ns, name);
    if(symbol != nullptr)
    {
        return symbol;
    }

    usize i;
    for(i = 0; i < list_size(&parser->visible_parsers); i = i + 1)
    {
        Parser* visible_parser = list_get(&parser->visible_parsers, i);

        symbol = enum_table_find(&visible_parser->enum_table, ns, name);
        if(symbol != nullptr && symbol->exported)
        {
            return symbol;
        }
    }

    return nullptr;
}

export StructSymbol* parser_find_struct_symbol_r(Parser* parser, Namespace* ns,
    char* name)
{
    StructSymbol* symbol = parser_find_struct_symbol(parser, ns, name);
    while(symbol == nullptr && ns != nullptr)
    {
        ns = ns->parent;
        symbol = parser_find_struct_symbol(parser, ns, name);
    }

    return symbol;
}

export StructSymbol* parser_find_struct_symbol(Parser* parser, Namespace* ns,
    char* name)
{
    assert(parser != nullptr);

    StructSymbol* symbol = struct_table_find(&parser->struct_table, ns, name);
    if(symbol != nullptr)
    {
        return symbol;
    }

    usize i;
    for(i = 0; i < list_size(&parser->visible_parsers); i = i + 1)
    {
        Parser* visible_parser = list_get(&parser->visible_parsers, i);

        symbol = struct_table_find(&visible_parser->struct_table, ns, name);
        if(symbol != nullptr && symbol->exported)
        {
            return symbol;
        }
    }

    return nullptr;
}

export FuncSymbol* parser_find_func_symbol_r(Parser* parser, Namespace* ns,
    char* name)
{
    FuncSymbol* symbol = parser_find_func_symbol(parser, ns, name);
    while(symbol == nullptr && ns != nullptr)
    {
        ns = ns->parent;
        symbol = parser_find_func_symbol(parser, ns, name);
    }

    return symbol;
}

export FuncSymbol* parser_find_func_symbol(Parser* parser, Namespace* ns,
    char* name)
{
    assert(parser != nullptr);

    FuncSymbol* symbol = func_table_find(&parser->func_table, ns, name);
    if(symbol != nullptr)
    {
        return symbol;
    }

    usize i;
    for(i = 0; i < list_size(&parser->visible_parsers); i = i + 1)
    {
        Parser* visible_parser = list_get(&parser->visible_parsers, i);

        symbol = func_table_find(&visible_parser->func_table, ns, name);
        if(symbol != nullptr && symbol->exported)
        {
            return symbol;
        }
    }

    return nullptr;
}

export SourceLocation parser_current_location(Parser* parser)
{
    assert(parser != nullptr);

    return parser->token->range.start;
}

export char* parser_source_file(Parser* parser)
{
    assert(parser != nullptr);

    return lexer_source_file(&parser->lexer);
}

export void parser_destroy(Parser* parser)
{
    assert(parser != nullptr);

    lexer_destroy(&parser->lexer);
}

void parser_push_namespace(Parser* parser, char* name)
{
    assert(parser != nullptr);

    parser->current_namespace = namespace_create(parser->current_namespace,
        name);
}

void parser_pop_namespace(Parser* parser)
{
    assert(parser != nullptr);

    assert(parser->current_namespace != nullptr);

    parser->current_namespace = parser->current_namespace->parent;
}

void parser_push_scope(Parser* parser)
{
    assert(parser != nullptr);

    parser->current_scope = scope_create(parser->current_scope);
}

void parser_pop_scope(Parser* parser)
{
    assert(parser != nullptr);

    parser->current_scope = parser->current_scope->parent;
}
