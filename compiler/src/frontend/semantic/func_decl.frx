import "func_params.frx";
import "type_specifier.frx";
import "../core/errors.frx";
import "../core/parser.frx";
import "../symbols/func.frx";
import "../syntax/func_decl.frx";
import "../../core/list.frx";

export void analyze_func_decl(mut Parser* parser, mut ASTFuncDecl* func_decl)
{
    frx::assert(parser != nullptr);

    frx::assert(func_decl != nullptr);

    analyze_type_specifier(parser, func_decl->return_type);

    FuncSymbol* symbol = &func_decl->symbol;

    mut usize i;
    for(i = 0; i < list_size(&parser->visible_parsers)
        && !func_decl->is_redefinition; i = i + 1)
    {
        Parser* visible_parser = list_get(&parser->visible_parsers, i);
        FuncSymbol* imported_symbol = func_table_find(
            &visible_parser->func_table, symbol->ns, symbol->name);

        func_decl->is_redefinition = imported_symbol != nullptr
            && imported_symbol->exported;
    }

    if(func_decl->is_redefinition)
    {
        error_func_decl_redefinition(parser_source_file(parser),
            func_decl->range.start, symbol->name);

        parser->failed = true;
    }

    analyze_func_params(parser, func_decl->params);
}
