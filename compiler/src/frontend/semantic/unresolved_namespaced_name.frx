import "ast.frx";
import "../core/parser.frx";
import "../syntax/unresolved_namespaced_name.frx";

export void analyze_unresolved_namespaced_name(Parser* parser,
    ASTUnresolvedNamespacedName* unresolved_namespaced_name, AST* ast)
{
    assert(parser != nullptr);

    assert(unresolved_namespaced_name != nullptr);

    Namespace* ns = unresolved_namespaced_name->ns;
    char* name = unresolved_namespaced_name->name;
    Scope* scope = unresolved_namespaced_name->scope;

    EnumSymbol* enum_symbol = parser_find_enum_symbol(parser, ns->parent,
        ns->name);

    b8 is_enum_constant = enum_symbol != nullptr && enum_symbol_has_constant(
        enum_symbol, name);

    b8 is_global_var = var_table_find_by_namespace(&parser->var_table, ns, name)
        != nullptr;

    b8 is_ambiguous = (is_enum_constant + is_global_var) > 1;
    b8 resolved = is_enum_constant || is_global_var;

    if(is_ambiguous)
    {
        error_ambiguous_unresolved_namespaced_name(parser_source_file(parser),
            unresolved_namespaced_name->range.start, ns, name);
        parser->failed = true;
    }
    else if(is_enum_constant)
    {
        ast->type = AST_TYPE_ENUM_CONSTANT;
        ast->node = make_enum_constant(&parser->arena, ns->parent, ns->name,
            name, scope);
    }
    else if(is_global_var)
    {
        ast->type = AST_TYPE_VAR;
        ast->node = make_var(&parser->arena, ns, scope, name);
    }
    else
    {
        error_unresolved_namespaced_name(parser_source_file(parser),
            unresolved_namespaced_name->range.start, ns, name);
        parser->failed = true;
    }

    if(!is_ambiguous && resolved)
    {
        analyze_ast(parser, ast);
    }
}
