import "std/libc.frx";
import "../core/namespace.frx";
import "../core/scope.frx";
import "../core/token.frx";
import "../ast/enum_def.frx";
import "../../core/hash.frx";
export import "../../core/list.frx";

export struct EnumSymbol
{
    TokenType type;
    core::Scope* scope;
    core::Namespace* ns;
    char* name;
    List constants;
    b8 exported;
}

export void enum_symbol_init(mut EnumSymbol* symbol, TokenType type,
    core::Scope* scope, core::Namespace* ns, char* name, b8 exported)
{
    frx::assert(symbol != nullptr);

    frx::assert(type != TokenType::EOF && type < TokenType::COUNT);

    frx::assert(scope != nullptr);

    frx::assert(name != nullptr);

    symbol->type = type;
    symbol->scope = scope;
    symbol->ns = ns;
    symbol->name = name;
    list_init(&symbol->constants);
    symbol->exported = exported;
}

export b8 enum_symbol_has_constant(EnumSymbol* symbol, char* name)
{
    frx::assert(symbol != nullptr);

    frx::assert(name != nullptr);

    mut usize i;
    for(i = 0; i < list_size(&symbol->constants); i = i + 1)
    {
        AST::EnumConstantDef* constant = list_get(&symbol->constants, i);
        if(libc::strcmp(constant->name, name) == 0)
        {
            return true;
        }
    }

    return false;
}

struct EnumTableEntry
{
    EnumSymbol* symbol;
    EnumTableEntry* next;
}

enum Constants : u64
{
    ENUM_TABLE_CAPACITY = 128
}

export struct EnumTable
{
    EnumTableEntry* entries[Constants::ENUM_TABLE_CAPACITY];
}

export void enum_table_init(EnumTable* table)
{
    frx::assert(table != nullptr);

    libc::memset(table, 0, frx::sizeof(EnumTable));
}

export EnumSymbol* enum_table_find_r(EnumTable* table, mut core::Namespace* ns,
    char* name)
{
    mut EnumSymbol* symbol = enum_table_find(table, ns, name);
    while(symbol == nullptr && ns != nullptr)
    {
        ns = ns->parent;
        symbol = enum_table_find(table, ns, name);
    }

    return symbol;
}

export EnumSymbol* enum_table_find(EnumTable* table, core::Namespace* ns, char* name)
{
    frx::assert(table != nullptr);

    frx::assert(name != nullptr);

    u64 index = hash_djb2(name) % Constants::ENUM_TABLE_CAPACITY;
    mut EnumTableEntry* entry = table->entries[index];

    while(entry != nullptr)
    {
        EnumSymbol* symbol = entry->symbol;

        if(libc::strcmp(symbol->name, name) == 0 && namespace_equals(symbol->ns, ns))
        {
            return symbol;
        }

        entry = entry->next;
    }

    return nullptr;
}

export b8 enum_table_insert(EnumTable* table, EnumSymbol* symbol)
{
    frx::assert(table != nullptr);

    frx::assert(symbol != nullptr);

    u64 index = hash_djb2(symbol->name) % Constants::ENUM_TABLE_CAPACITY;
    mut EnumTableEntry* entry = table->entries[index];

    while(entry != nullptr)
    {
        EnumSymbol* existing_symbol = entry->symbol;

        if(libc::strcmp(existing_symbol->name, symbol->name) == 0
            && namespace_equals(existing_symbol->ns, symbol->ns))
        {
            return false;
        }

        entry = entry->next;
    }

    mut EnumTableEntry* new_entry = libc::malloc(frx::sizeof(EnumTableEntry));
    new_entry->symbol = symbol;
    new_entry->next = table->entries[index];

    table->entries[index] = new_entry;

    return true;
}
