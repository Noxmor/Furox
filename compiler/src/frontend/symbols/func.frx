import "std/libc.frx";
export import "../core/namespace.frx";
export import "../core/scope.frx";
import "../../core/hash.frx";

export struct FuncSymbol
{
    core::Scope* scope;
    core::Namespace* ns;
    char* name;
    b8 exported;
    b8 external;
}

export void func_symbol_init(mut FuncSymbol* symbol, core::Scope* scope,
    core::Namespace* ns, char* name, b8 exported, b8 external)
{
    frx::assert(symbol != nullptr);

    frx::assert(scope != nullptr);

    frx::assert(name != nullptr);

    symbol->scope = scope;
    symbol->ns = ns;
    symbol->name = name;
    symbol->exported = exported;
    symbol->external = external;
}

struct FuncTableEntry
{
    FuncSymbol* symbol;
    FuncTableEntry* next;
}

enum Constants : u64
{
    FUNC_TABLE_CAPACITY = 128
}

export struct FuncTable
{
    FuncTableEntry* entries[Constants::FUNC_TABLE_CAPACITY];
}

export void func_table_init(FuncTable* table)
{
    frx::assert(table != nullptr);

    libc::memset(table, 0, frx::sizeof(FuncTable));
}

export FuncSymbol* func_table_find_r(FuncTable* table, mut core::Namespace* ns,
    char* name)
{
    mut FuncSymbol* symbol = func_table_find(table, ns, name);
    while(symbol == nullptr && ns != nullptr)
    {
        ns = ns->parent;
        symbol = func_table_find(table, ns, name);
    }

    return symbol;
}

export FuncSymbol* func_table_find(FuncTable* table, core::Namespace* ns, char* name)
{
    frx::assert(table != nullptr);

    frx::assert(name != nullptr);

    u64 index = hash_djb2(name) % Constants::FUNC_TABLE_CAPACITY;
    mut FuncTableEntry* entry = table->entries[index];

    while(entry != nullptr)
    {
        FuncSymbol* symbol = entry->symbol;

        if(libc::strcmp(symbol->name, name) == 0 && namespace_equals(symbol->ns, ns))
        {
            return symbol;
        }

        entry = entry->next;
    }

    return nullptr;
}

export b8 func_table_insert(FuncTable* table, FuncSymbol* symbol)
{
    frx::assert(table != nullptr);

    frx::assert(symbol != nullptr);

    u64 index = hash_djb2(symbol->name) % Constants::FUNC_TABLE_CAPACITY;
    mut FuncTableEntry* entry = table->entries[index];

    while(entry != nullptr)
    {
        FuncSymbol* existing_symbol = entry->symbol;

        if(libc::strcmp(existing_symbol->name, symbol->name) == 0
            && namespace_equals(existing_symbol->ns, symbol->ns))
        {
            return false;
        }

        entry = entry->next;
    }

    mut FuncTableEntry* new_entry = libc::malloc(frx::sizeof(FuncTableEntry));
    new_entry->symbol = symbol;
    new_entry->next = table->entries[index];

    table->entries[index] = new_entry;

    return true;
}
