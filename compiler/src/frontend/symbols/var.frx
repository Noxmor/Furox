import "std/libc.frx";
import "../core/namespace.frx";
import "../core/scope.frx";
import "../syntax/type_specifier.frx";
import "../../core/hash.frx";

export struct VarSymbol
{
    Namespace* ns;
    ASTTypeSpecifier* type;
    Scope* scope;
    char* name;
    b8 global;
    b8 mutable;
}

export void var_symbol_init(mut VarSymbol* symbol, Namespace* ns,
    ASTTypeSpecifier* type, Scope* scope, char* name, b8 global, b8 mutable)
{
    frx::assert(symbol != nullptr);

    frx::assert(type != nullptr);

    frx::assert(scope != nullptr);

    frx::assert(name != nullptr);

    symbol->ns = ns;
    symbol->type = type;
    symbol->scope = scope;
    symbol->name = name;
    symbol->global = global;
    symbol->mutable = mutable;
}

struct VarTableEntry
{
    VarSymbol* symbol;
    VarTableEntry* next;
}

enum Constants : u64
{
    VAR_TABLE_CAPACITY = 128
}

export struct VarTable
{
    VarTableEntry* entries[Constants::VAR_TABLE_CAPACITY];
}

export void var_table_init(VarTable* table)
{
    frx::assert(table != nullptr);

    libc::memset(table, 0, frx::sizeof(VarTable));
}

export VarSymbol* var_table_find_by_namespace(VarTable* table, Namespace* ns,
    char* name)
{
    frx::assert(table != nullptr);

    frx::assert(name != nullptr);

    u64 index = hash_djb2(name) % Constants::VAR_TABLE_CAPACITY;
    mut VarTableEntry* entry = table->entries[index];

    while(entry != nullptr)
    {
        VarSymbol* symbol = entry->symbol;

        if(libc::strcmp(symbol->name, name) == 0 && namespace_equals(symbol->ns, ns))
        {
            return symbol;
        }

        entry = entry->next;
    }

    return nullptr;
}

export VarSymbol* var_table_find_by_scope(VarTable* table, Scope* scope,
    char* name)
{
    frx::assert(table != nullptr);

    frx::assert(scope != nullptr);

    frx::assert(name != nullptr);

    u64 index = hash_djb2(name) % Constants::VAR_TABLE_CAPACITY;
    mut VarTableEntry* entry = table->entries[index];

    while(entry != nullptr)
    {
        VarSymbol* symbol = entry->symbol;

        if(libc::strcmp(symbol->name, name) == 0
            && scope_is_child_of(scope, symbol->scope))
        {
            return symbol;
        }

        entry = entry->next;
    }

    return nullptr;
}

export b8 var_table_insert(VarTable* table, VarSymbol* symbol)
{
    frx::assert(table != nullptr);

    frx::assert(symbol != nullptr);

    u64 index = hash_djb2(symbol->name) % Constants::VAR_TABLE_CAPACITY;
    mut VarTableEntry* entry = table->entries[index];

    while(entry != nullptr)
    {
        VarSymbol* existing_symbol = entry->symbol;

        if(libc::strcmp(existing_symbol->name, symbol->name) == 0
            && scope_is_child_of(symbol->scope, existing_symbol->scope))
        {
            return false;
        }

        entry = entry->next;
    }

    mut VarTableEntry* new_entry = libc::malloc(frx::sizeof(VarTableEntry));
    new_entry->symbol = symbol;
    new_entry->next = table->entries[index];

    table->entries[index] = new_entry;

    return true;
}
