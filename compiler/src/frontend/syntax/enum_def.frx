import "binary_expr.frx";
import "expr.frx";
import "int_literal.frx";
import "../core/ast.frx";
import "../core/namespace.frx";
import "../core/parser.frx";
import "../core/scope.frx";
import "../symbols/enum.frx";
import "../../core/list.frx";

extern struct FILE;

extern
{
    i32 fprintf(mut FILE* stream, char* format, ...);
}

export struct ASTEnumConstantDef
{
    char* name;
    AST* value;
}

ASTEnumConstantDef* make_enum_constant_def(Arena* arena, char* name, AST* value)
{
    frx::assert(name != nullptr);

    mut ASTEnumConstantDef* constant = arena_alloc(arena,
        frx::sizeof(ASTEnumConstantDef));

    constant->name = name;
    constant->value = value;

    return constant;
}

ASTEnumConstantDef* parse_enum_constant(Parser* parser)
{
    frx::assert(parser != nullptr);

    char* name = parser->token->identifier;

    parser_eat(parser, TokenType::IDENTIFIER);

    mut AST* value = nullptr;
    if(parser->token->type == TokenType::EQ)
    {
        parser_eat(parser, TokenType::EQ);

        value = parse_expr(parser);
    }

    return make_enum_constant_def(&parser->arena, name, value);
}

export struct ASTEnumDef
{
    SourceRange range;

    EnumSymbol symbol;

    b8 is_redefinition;
}

void enum_def_add_constant(Arena* arena, ASTEnumDef* enum_def,
    mut ASTEnumConstantDef* constant)
{
    frx::assert(enum_def != nullptr);

    frx::assert(constant != nullptr);

    List* constants = &enum_def->symbol.constants;

    if(constant->value == nullptr)
    {
        mut AST* new_value = nullptr;

        if(list_size(constants) == 0)
        {
            ASTIntLiteral* literal = make_int_literal(arena, 0);
            new_value = make_ast(arena, AstType::INT_LITERAL, literal);
        }
        else
        {
            ASTIntLiteral* literal = make_int_literal(arena, 1);

            ASTEnumConstantDef* last_constant = list_get(constants,
                list_size(constants) - 1);

            AST* left = last_constant->value;
            AST* right = make_ast(arena, AstType::INT_LITERAL, literal);

            ASTBinaryExpr* expr = make_binary_expr(arena, TokenType::PLUS,
                Operator::ADD, left, right);

            new_value = make_ast(arena, AstType::BINARY_EXPR, expr);
        }

        constant->value = new_value;
    }

    list_push(constants, constant);
}

export ASTEnumDef* make_enum_def(Arena* arena, Scope* scope, Namespace* ns,
    char* name, TokenType type, b8 exported)
{
    mut ASTEnumDef* enum_def = arena_alloc(arena, frx::sizeof(ASTEnumDef));

    enum_symbol_init(&enum_def->symbol, type, scope, ns, name, exported);

    enum_def->is_redefinition = false;

    return enum_def;
}

export ASTEnumDef* parse_enum_def(Parser* parser, b8 exported)
{
    frx::assert(parser != nullptr);

    SourceLocation start = parser_current_location(parser);

    parser_eat(parser, TokenType::KW_ENUM);

    char* name = parser->token->identifier;

    parser_eat(parser, TokenType::IDENTIFIER);

    parser_eat(parser, TokenType::COLON);

    mut TokenType type = TokenType::ERROR;

    switch(parser->token->type)
    {
        case TokenType::KW_I8:
        {

        }
        case TokenType::KW_I16:
        {

        }
        case TokenType::KW_I32:
        {

        }
        case TokenType::KW_I64:
        {

        }
        case TokenType::KW_U8:
        {

        }
        case TokenType::KW_U16:
        {

        }
        case TokenType::KW_U32:
        {

        }
        case TokenType::KW_U64:
        {
            type = parser->token->type;

            parser_eat(parser, parser->token->type);

            break;
        }
        default:
        {
            parser_panic(parser);

            break;
        }
    }

    mut ASTEnumDef* enum_def = make_enum_def(&parser->arena,
        parser->current_scope, parser->current_namespace, name, type, exported);

    enum_def->is_redefinition = !enum_table_insert(&parser->enum_table,
        &enum_def->symbol);

    parser_eat(parser, TokenType::LBRACE);

    while(parser_loop_until(parser, TokenType::RBRACE))
    {
        ASTEnumConstantDef* constant = parse_enum_constant(parser);

        enum_def_add_constant(&parser->arena, enum_def, constant);

        if(parser->token->type == TokenType::COMMA)
        {
            parser_eat(parser, TokenType::COMMA);
        }
        else
        {
            break;
        }
    }

    enum_def->range.start = start;
    enum_def->range.end = parser_current_location(parser);

    parser_eat(parser, TokenType::RBRACE);

    return enum_def;
}

export void print_enum_def(ASTEnumDef* enum_def, FILE* file, usize depth)
{
    frx::assert(enum_def != nullptr);

    frx::assert(file != nullptr);

    print_ast_depth(file, depth);

    fprintf(file, "%s (%s)\n", ast_type_to_str(AstType::ENUM_DEF),
        enum_def->symbol.name);

    mut usize i;
    for(i = 0; i < list_size(&enum_def->symbol.constants); i = i + 1)
    {
        ASTEnumConstantDef* constant = list_get(&enum_def->symbol.constants, i);

        print_ast_depth(file, depth + 1);
        fprintf(file, "%s\n", constant->name);
    }
}
