import "../core/ast.frx";
import "../core/namespace.frx";
import "../core/parser.frx";
import "../core/source_location.frx";
import "../core/token.frx";
import "binary_expr.frx";
import "char_literal.frx";
import "error.frx";
import "float_literal.frx";
import "func_call.frx";
import "int_literal.frx";
import "internal_func_call.frx";
import "namespace_resolution.frx";
import "string_literal.frx";
import "unary_expr.frx";
import "unresolved_name.frx";
import "unresolved_namespaced_name.frx";

export AST* parse_primary_expr(Parser* parser)
{
    frx::assert(parser != nullptr);

    mut AST* primary_expr = nullptr;

    switch(parser->token->type)
    {
        case TokenType::IDENTIFIER:
        {
            SourceLocation start = parser_current_location(parser);
            Namespace* resolved_ns = parse_namespace_resolution(parser);

            Token* next = parser_peek(parser, 1);

            if(next->type == TokenType::LPAREN)
            {
                if(namespace_is_internal(resolved_ns))
                {
                    primary_expr = parse_internal_func_call(parser);
                }
                else
                {
                    primary_expr = make_ast(&parser->arena, AstType::FUNC_CALL,
                        parse_func_call(parser, resolved_ns));
                }
            }
            else if(resolved_ns == nullptr)
            {
                primary_expr = make_ast(&parser->arena,
                    AstType::UNRESOLVED_NAME, parse_unresolved_name(parser));
            }
            else
            {
                primary_expr = make_ast(&parser->arena,
                    AstType::UNRESOLVED_NAMESPACED_NAME,
                    parse_unresolved_namespaced_name(parser, resolved_ns,
                    start));
            }

            break;
        }
        case TokenType::NAMESPACE_RESOLUTION:
        {
            Namespace* resolved_ns = parse_namespace_resolution(parser);

            primary_expr = make_ast(&parser->arena, AstType::FUNC_CALL,
                parse_func_call(parser, resolved_ns));

            break;
        }
        case TokenType::KW_NULLPTR:
        {
            parser_eat(parser, TokenType::KW_NULLPTR);

            primary_expr = make_ast(&parser->arena, AstType::INT_LITERAL,
                make_int_literal(&parser->arena, 0));

            break;
        }
        case TokenType::KW_TRUE:
        {
            parser_eat(parser, TokenType::KW_TRUE);

            primary_expr = make_ast(&parser->arena, AstType::INT_LITERAL,
                make_int_literal(&parser->arena, 1));

            break;
        }
        case TokenType::KW_FALSE:
        {
            parser_eat(parser, TokenType::KW_FALSE);

            primary_expr = make_ast(&parser->arena, AstType::INT_LITERAL,
                make_int_literal(&parser->arena, 0));

            break;
        }
        case TokenType::FLOAT_LITERAL:
        {
            primary_expr = make_ast(&parser->arena, AstType::FLOAT_LITERAL,
                parse_float_literal(parser));

            break;
        }
        case TokenType::INT_LITERAL:
        {
            primary_expr = make_ast(&parser->arena, AstType::INT_LITERAL,
                parse_int_literal(parser));

            break;
        }
        case TokenType::CHAR_LITERAL:
        {
            primary_expr = make_ast(&parser->arena, AstType::CHAR_LITERAL,
                parse_char_literal(parser));

            break;
        }
        case TokenType::STRING_LITERAL:
        {
            primary_expr = make_ast(&parser->arena, AstType::STRING_LITERAL,
                parse_string_literal(parser));

            break;
        }
        default:
        {
            primary_expr = make_ast(&parser->arena, AstType::ERROR,
                parse_error(parser));

            break;
        }
    }

    return primary_expr;
}

export AST* parse_expr(Parser* parser)
{
    return parse_expr_with_precedence(parser, Precedence::MIN);
}

export AST* parse_expr_with_precedence(Parser* parser,
    Precedence min_precedence)
{
    frx::assert(parser != nullptr);

    mut AST* expr = nullptr;

    if(parser->token->type == TokenType::LPAREN)
    {
        parser_eat(parser, TokenType::LPAREN);
        expr = parse_expr(parser);
        parser_eat(parser, TokenType::RPAREN);
    }
    else if(token_type_is_prefix_operator(parser->token->type))
    {
        TokenType type = parser->token->type;

        parser_eat(parser, type);

        Operator operator = token_type_to_prefix_operator(type);
        Precedence precedence = operator_to_precedence(operator);
        ASTUnaryExpr* unary_expr = make_unary_expr(&parser->arena, type,
            operator, parse_expr_with_precedence(parser, precedence));
        expr = make_ast(&parser->arena, AstType::UNARY_EXPR, unary_expr);
    }
    else
    {
        expr = parse_primary_expr(parser);
    }

    frx::assert(expr != nullptr);

    while(true)
    {
        TokenType type = parser->token->type;

        if(token_type_is_postfix_operator(type))
        {
            Operator operator = token_type_to_postfix_operator(type);
            Precedence precedence = operator_to_precedence(operator);
            if(precedence >= min_precedence)
            {
                break;
            }

            parser_eat(parser, type);

            if(operator == Operator::ARRAY_SUBSCRIPT)
            {
                AST* index = parse_expr(parser);
                parser_eat(parser, TokenType::RBRACKET);

                ASTBinaryExpr* binary_expr = make_binary_expr(&parser->arena,
                    type, operator, expr, index);
                expr = make_ast(&parser->arena, AstType::BINARY_EXPR,
                    binary_expr);
            }
            else
            {
                ASTUnaryExpr* unary_expr = make_unary_expr(&parser->arena, type,
                    operator, expr);
                expr = make_ast(&parser->arena, AstType::UNARY_EXPR,
                    unary_expr);
            }

            continue;
        }
        else if(token_type_is_infix_operator(type))
        {
            Operator operator = token_type_to_infix_operator(type);
            Precedence precedence = operator_to_precedence(operator);
            if(precedence >= min_precedence)
            {
                break;
            }

            parser_eat(parser, type);

            ASTBinaryExpr* binary_expr = make_binary_expr(&parser->arena, type,
                operator, expr, parse_expr_with_precedence(parser, precedence));
            expr = make_ast(&parser->arena, AstType::BINARY_EXPR, binary_expr);

            continue;
        }

        break;
    }

    return expr;
}
