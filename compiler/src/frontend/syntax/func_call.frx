import "expr.frx";
import "namespace_resolution.frx";
import "../core/ast.frx";
import "../core/namespace.frx";
import "../core/parser.frx";
import "../core/scope.frx";
import "../symbols/func.frx";
import "../../core/list.frx";

extern struct FILE;

extern
{
    i32 fprintf(mut FILE* stream, char* format, ...);
}

export struct ASTFuncCall
{
    FuncSymbol* symbol;

    Namespace* ns;
    char* name;

    Scope* scope;

    List args;
}

export ASTFuncCall* make_func_call(Arena* arena, Namespace* ns, char* name,
    Scope* scope)
{
    frx::assert(name != nullptr);

    frx::assert(scope != nullptr);

    mut ASTFuncCall* func_call = arena_alloc(arena, frx::sizeof(ASTFuncCall));

    func_call->symbol = nullptr;
    func_call->ns = ns;
    func_call->name = name;
    func_call->scope = scope;
    list_init(&func_call->args);

    return func_call;
}

export ASTFuncCall* parse_func_call(Parser* parser, Namespace* ns)
{
    frx::assert(parser != nullptr);

    char* name = parser->token->identifier;

    parser_eat(parser, TokenType::IDENTIFIER);

    ASTFuncCall* func_call = make_func_call(&parser->arena, ns, name,
        parser->current_scope);

    parser_eat(parser, TokenType::LPAREN);

    while(parser_loop_until(parser, TokenType::RPAREN))
    {
        if(list_size(&func_call->args) > 0)
        {
            parser_eat(parser, TokenType::COMMA);
        }

        AST* arg = parse_expr(parser);
        list_push(&func_call->args, arg);
    }

    parser_eat(parser, TokenType::RPAREN);

    return func_call;
}

export void print_func_call(ASTFuncCall* func_call, FILE* file, usize depth)
{
    frx::assert(func_call != nullptr);

    frx::assert(file != nullptr);

    print_ast_depth(file, depth);

    fprintf(file, "%s (", ast_type_to_str(AstType::FUNC_CALL));
    print_namespace_resolution(file, func_call->ns);
    fprintf(file, "%s)\n", func_call->name);

    mut usize i;
    for(i = 0; i < list_size(&func_call->args); i = i + 1)
    {
        AST* ast = list_get(&func_call->args, i);
        print_ast(ast, file, depth + 1);
    }
}
