export import "../ast/if_stmt.frx";
import "expr.frx";
import "scope.frx";
import "../core/parser.frx";

export IfStmt* if_stmt_parse(Parser* parser)
{
    frx::assert(parser != nullptr);

    parser_eat(parser, TokenType::KW_IF);

    parser_eat(parser, TokenType::LPAREN);
    Expr* condition = expr_parse(parser);
    parser_eat(parser, TokenType::RPAREN);
    Scope* if_block = scope_parse(parser);

    mut Scope* else_block = nullptr;
    if(parser->token->type == TokenType::KW_ELSE)
    {
        parser_eat(parser, TokenType::KW_ELSE);

        if(parser->token->type == TokenType::KW_IF)
        {
            else_block = scope_new(&parser->arena);
            scope_add_stmt(else_block,
                    stmt_from_if_stmt(&parser->arena, if_stmt_parse(parser)));
        }
        else
        {
            else_block = scope_parse(parser);
        }
    }

    return if_stmt_new(&parser->arena, condition, if_block, else_block);
}
