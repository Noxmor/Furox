import "expr.frx";
import "scope.frx";
import "../core/ast.frx";
import "../core/parser.frx";

extern struct FILE;

extern
{
    i32 fprintf(mut FILE* stream, char* format, ...);
}

export struct ASTIfStmt
{
    AST* condition;
    ASTScope* if_block;
    ASTScope* else_block;
}

ASTIfStmt* make_if_stmt(Arena* arena, AST* condition, ASTScope* if_block,
    ASTScope* else_block)
{
    mut ASTIfStmt* if_stmt = arena_alloc(arena, frx::sizeof(ASTIfStmt));

    if_stmt->condition = condition;
    if_stmt->if_block = if_block;
    if_stmt->else_block = else_block;

    return if_stmt;
}

export ASTIfStmt* parse_if_stmt(Parser* parser)
{
    frx::assert(parser != nullptr);

    parser_eat(parser, TokenType::KW_IF);

    parser_eat(parser, TokenType::LPAREN);
    AST* condition = parse_expr(parser);
    parser_eat(parser, TokenType::RPAREN);
    ASTScope* if_block = parse_scope(parser);

    mut ASTScope* else_block = nullptr;
    if(parser->token->type == TokenType::KW_ELSE)
    {
        parser_eat(parser, TokenType::KW_ELSE);

        if(parser->token->type == TokenType::KW_IF)
        {
            else_block = make_scope(&parser->arena);
            list_push(&else_block->stmts, make_ast(&parser->arena,
                AstType::IF_STMT, parse_if_stmt(parser)));
        }
        else
        {
            else_block = parse_scope(parser);
        }
    }

    return make_if_stmt(&parser->arena, condition, if_block, else_block);
}

export void print_if_stmt(ASTIfStmt* if_stmt, FILE* file, usize depth)
{
    frx::assert(if_stmt != nullptr);

    frx::assert(file != nullptr);

    print_ast_depth(file, depth);

    fprintf(file, "%s\n", ast_type_to_str(AstType::IF_STMT));
    print_ast(if_stmt->condition, file, depth + 1);
    print_scope(if_stmt->if_block, file, depth + 1);

    if(if_stmt->else_block != nullptr)
    {
        print_scope(if_stmt->else_block, file, depth + 1);
    }
}
