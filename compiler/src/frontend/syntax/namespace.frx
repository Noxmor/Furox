import "global_scope.frx";
import "../core/ast.frx";
import "../core/namespace.frx";
import "../core/parser.frx";
import "../../core/list.frx";

extern struct FILE;

extern
{
    i32 fprintf(mut FILE* stream, char* format, ...);
}

export struct ASTNamespace
{
    Namespace* ns;

    List global_scope;
}

export ASTNamespace* make_namespace(Arena* arena, Namespace* ns)
{
    frx::assert(ns != nullptr);

    mut ASTNamespace* namespace_node = arena_alloc(arena,
        frx::sizeof(ASTNamespace));

    namespace_node->ns = ns;

    list_init(&namespace_node->global_scope);

    return namespace_node;
}

export ASTNamespace* parse_namespace(Parser* parser)
{
    frx::assert(parser != nullptr);

    Namespace* old_namespace = parser->current_namespace;

    parser_eat(parser, TokenType::KW_NAMESPACE);

    parser_push_namespace(parser, parser->token->identifier);

    parser_eat(parser, TokenType::IDENTIFIER);

    ASTNamespace* namespace_root = make_namespace(&parser->arena,
        parser->current_namespace);

    mut ASTNamespace* current_namespace = namespace_root;

    while(parser->token->type == TokenType::NAMESPACE_RESOLUTION)
    {
        parser_eat(parser, TokenType::NAMESPACE_RESOLUTION);

        parser_push_namespace(parser, parser->token->identifier);

        parser_eat(parser, TokenType::IDENTIFIER);

        ASTNamespace* child_namespace = make_namespace(&parser->arena,
            parser->current_namespace);

        AST* ast = make_ast(&parser->arena, AstType::NAMESPACE,
            child_namespace);

        list_push(&current_namespace->global_scope, ast);

        current_namespace = child_namespace;
    }

    parser_eat(parser, TokenType::LBRACE);

    parser_push_scope(parser);

    while(parser_loop_until(parser, TokenType::RBRACE))
    {
        AST* child = parse_global_scope(parser);
        list_push(&current_namespace->global_scope, child);
    }

    parser_eat(parser, TokenType::RBRACE);

    pop_back_namespace_until(parser, old_namespace);
    parser_pop_scope(parser);

    return namespace_root;
}

export void print_namespace(ASTNamespace* namespace_node, FILE* file,
usize depth)
{
    frx::assert(namespace_node != nullptr);

    frx::assert(file != nullptr);

    print_ast_depth(file, depth);

    fprintf(file, "%s (%s)\n", ast_type_to_str(AstType::NAMESPACE),
        namespace_node->ns->name);

    mut usize i;
    for(i = 0; i < list_size(&namespace_node->global_scope); i = i + 1)
    {
        AST* ast = list_get(&namespace_node->global_scope, i);
        print_ast(ast, file, depth + 1);
    }
}

void pop_back_namespace_until(Parser* parser, Namespace* old_namespace)
{
    frx::assert(parser != nullptr);

    while(parser->current_namespace != old_namespace)
    {
        parser_pop_namespace(parser);
    }
}
